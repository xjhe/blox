plugins {
    id "java"
    id "blox-deploy"
}

group 'com.amazonaws.blox'
version '0.1-SNAPSHOT'

sourceCompatibility = 1.8

dependencies {
    compile project(":frontend-service-client")

    compile "software.amazon.awssdk:ecs:2.0.0-preview-4"
    compile "software.amazon.awssdk:cloudformation:2.0.0-preview-4"
    compile "org.projectlombok:lombok:1.16.18"

    testCompile "org.assertj:assertj-core:3.8.0"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test { onlyIf { false } }

deployment {
    aws {
        profile stack.profile.toString()
        region stack.region.toString()
    }

    stackName "blox-test-cluster"
    s3Bucket stack.s3Bucket.toString()

    templateFile file("templates/test_cluster.yml")
}

task testEndToEnd(type: Test) {
    group "verification"
    description "Run end to end integration tests"

    dependsOn ":end-to-end-tests:deploy"
    dependsOn ":frontend-service:deploy"
    dependsOn ":data-service:deploy"
    dependsOn ":scheduling-manager:deploy"

    doFirst {
        systemProperty 'aws.region', stack.region.toString()
        systemProperty 'aws.defaultProfile', stack.profile.toString()
        systemProperty 'blox.tests.apiUrl', findProject(":frontend-service").deployment.outputs["ApiUrl"]
    }
}

