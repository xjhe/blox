plugins {
    id 'java'
    id "com.commercehub.cucumber-jvm"
}

repositories {
    mavenCentral()
}

addCucumberSuite 'cucumberTest'

cucumberTest {
    tags = ["~@ignore"]

    dependsOn ":data-service:deploy"

    systemProperties = [
            'aws.region': stack.region.toString(),
            'aws.defaultProfile': stack.profile.toString(),

            // Suppress the Spring Context's verbose startup logs that usualy get
            // emitted on each scenario run, but still warn when things go wrong.
            // If you want to debug Spring issues, set this to info or debug.
            'org.slf4j.simpleLogger.log.org.springframework': 'warn',
    ]
}

dependencies {
    compile project(":data-service-client")

    cucumberTestCompile(
        'com.google.guava:guava:23.0',
        'info.cukes:cucumber-java8:1.2.5',
        'info.cukes:cucumber-spring:1.2.5',

        // Wire the Spring Framework's JCL logs into SLF4J
        'org.slf4j:jcl-over-slf4j',
        // Use the SLF4J simpleLogger for logging in tests
        'org.slf4j:slf4j-simple',

        'junit:junit:4.12',
        'org.assertj:assertj-core:3.8+',

        'org.apache.commons:commons-lang3:3.6',
        'org.apache.commons:commons-text:1.1',
        'org.projectlombok:lombok:1.16.18',

        'org.springframework:spring-beans',
        'org.springframework:spring-context',
        'org.springframework:spring-test',
        'software.amazon.awssdk:cloudformation:2.0.0-preview-4'
    )
}

